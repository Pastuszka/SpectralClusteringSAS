/* Michal Pastuszka & Wojciech Bogucki (2020). Spectral Clustering for SAS */
/* spccKNNNeigh: Transform a complete neighborhood matrix to a knn neighborhood matrix
   connecting only vertices where at least one belongs to the k nearest neighbors
   of the other
   Input:
   Required:
   neigh         a complete neighborhood matrix
   Optional:
   k             number of nearest neghbors to leave connected. default = 10
*/
start spccKNNNeigh(neigh, k=10);
	n = ncol(neigh);
	do i=1 to n;
		y = neigh[loc(rank(neigh[,i])=n-k+1),i];
		neigh[loc(neigh[,i]<y),i] = 0;
	end;
	neigh = neigh <> neigh`;
	return neigh;
finish;